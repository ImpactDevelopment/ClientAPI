/*
 * Copyright 2017 ImpactDevelopment
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'SpongePowered'
            url = 'http://repo.spongepowered.org/maven'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.5-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.tweaker-client'
apply plugin: 'org.spongepowered.mixin'
apply from: 'methods.gradle'

version = getVersionFromJava()

sourceCompatibility = targetCompatibility = '1.8' // Needed so Eclipse task generates correctly
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = project.minecraftVersion
    mappings = project.mappings
    tweakClass = 'clientapi.load.ClientTweaker'
    runDir = 'run'

    makeObfSourceJar = false
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, 'mixins.capi.refmap.json'
    add sourceSets.test, 'mixins.client.refmap.json'
}

/**
 * Builds a CDK zip in builds/distributions using the cdk directory and variables from the gradle config
 */
task makeCDK(type: Zip) {
    baseName = project.name
    classifier = 'CDK'
    version = project.version
    destinationDir = file('build/distributions')

    // License
    from 'LICENSE'
    rename 'LICENSE', 'LICENSE.txt'

    // Include the gradle stuff
    from 'gradlew', 'gradlew.bat'
    into('gradle') { from 'gradle' }

    // Include the example code
    into('src/main') { from 'src/test' }

    // Include everything in cdk, and replace any @TOKEN@s
    from('cdk') {
        filter(ReplaceTokens, tokens: [
                VERSION : project.version,
                MCVERSION : minecraft.version,
                MAPPINGS: minecraft.mappings,
                TWEAKER: minecraft.tweakClass
        ])

        rename 'gitignore\\.txt', '.gitignore'
    }
}

tasks.build.dependsOn makeCDK

artifacts {
    archives makeCDK
}

repositories {
    mavenCentral()

    maven {
        name = 'impactdevelopment-repo'
        url = 'https://impactdevelopment.github.io/maven/'
    }

    maven {
        name = 'spongepowered-repo'
        url = 'http://repo.spongepowered.org/maven/'
    }
}

dependencies {
    compile 'com.github.ZeroMemes:Alpine:1.4'
    compile 'net.jodah:typetools:0.5.0'
    compile('org.spongepowered:mixin:0.7.1-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }
}

